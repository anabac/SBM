;**************************************************************************
; SBM 2015. ESTRUCTURA BÁSICA DE UN PROGRAMA EN ENSAMBLADOR
;**************************************************************************
; DEFINICION DEL SEGMENTO DE DATOS
DATOS SEGMENT
	N DW 138
DATOS ENDS
;**************************************************************************
; DEFINICION DEL SEGMENTO DE PILA
PILA SEGMENT STACK "STACK"
	DB 40H DUP (0) ;ejemplo de inicialización, 64 bytes inicializados a 0
PILA ENDS
;**************************************************************************
; DEFINICION DEL SEGMENTO EXTRA
EXTRA SEGMENT
	RESULT DB 5 DUP (?), '$'
EXTRA ENDS
;**************************************************************************
; DEFINICION DEL SEGMENTO DE CODIGO
CODE SEGMENT
	ASSUME CS: CODE, DS: DATOS, ES: EXTRA, SS: PILA
; COMIENZO DEL PROCEDIMIENTO PRINCIPAL
INICIO PROC
	; INICIALIZA LOS REGISTROS DE SEGMENTO CON SU VALOR
	MOV AX, DATOS
	MOV DS, AX
	MOV AX, PILA
	MOV SS, AX
	MOV AX, EXTRA
	MOV ES, AX
	MOV SP, 64 ; CARGA EL PUNTERO DE PILA CON EL VALOR MAS ALTO
	; FIN DE LAS INICIALIZACIONES
	; COMIENZO DEL PROGRAMA
	MOV BX, N ; GUARDA EL NUMERO EN BX

	CALL printASCII ; LLAMA A LA SUBRUTINA

	MOV BX, DS ; GUARDA EL COMIENZO DEL SEGMENTO DE DATOS PARA CONSERVARLO

	MOV DS, DX ; CAMBIA EL SEGMENTO DE DATOS POR AQUEL EN EL QUE ESTA EL RESULTADO
	MOV DX, AX ; GUARDA EN DX EL OFFSET DEL RESUTADO
	MOV AH, 9 ; OPERACION
	INT 21h ; IMPRIME POR PANTALLA

	MOV DS, BX ; VUELVE A DEJAR EL SEGMENTO DE DATOS ORIGINAL

	; FIN DEL PROGRAMA
	MOV AX, 4C00H
	INT 21H
INICIO ENDP
; COMIENZO DE LA SUBRUTINA
printASCII PROC NEAR
	MOV AX, BX ; GUARDA EL NUMERO EN AX
	MOV CX, 10 ; GUARDA 10 EN CL PARA LUEGO DIVIDIR
	MOV SI, 5 ; INICIALIZA EL INDICE A 

	DIGITS:
		DEC SI
		MOV DX, 0
		DIV CX ; DIVIDE EL CONTENIDO DE AX ENTRE 10. COCIENTE EN AX Y RESTO EN DX
		ADD DL, 30h ; CONVIERTE EL RESTO EN EL ASCII CONRRESPONDIENTE
		MOV RESULT[SI], DL ; LO GUARDA EN MEMORIA
		CMP AX, 0 ; SI NO SE HAN ACABADO LOS DIGITOS
		JNZ DIGITS ; SIGUE DIVIDIENDO

	MOV DX, SEG RESULT
	MOV AX, OFFSET RESULT
	ADD AX, SI

	RET
printASCII ENDP

; FIN DEL SEGMENTO DE CODIGO
CODE ENDS
; FIN DEL PROGRAMA INDICANDO DONDE COMIENZA LA EJECUCION
END INICIO 