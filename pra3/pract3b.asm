PRACT3B SEGMENT BYTE PUBLIC 'CODE'
ASSUME CS: PRACT3B
	PUBLIC _calcularAciertos, _calcularSemiaciertos
	_calcularAciertos PROC FAR
		PUSH BP
		MOV BP, SP ;COLOCAMOS EL PUNTERO EN LA CIMA DE LA PILA
		
		PUSH DI BX CX ;GUARDAMOS REGISTROS USADOS

		LES DI, [BP+6] ;SEGMENT=>ES OFFSET=>DI
		
		;METEMOS LOS 4 NUMEROS DEL INTENTO EN REGISTROS
		MOV BH, ES:[DI] 
		MOV BL, ES:[DI+1]
		MOV CH, ES:[DI+2]
		MOV CL, ES:[DI+3]

		;CAMBIAMOS AL CODIGO CORRECTO
		LES DI, [BP+10] ;SEGMENT=>ES OFFSET=>DI
		
		;COMPRUEBA SI COINCIDEN NUMEROS, Y SI COINCIDEN INCREMENTA UN CONTADOR
		MOV AX, 0

		CMP BH, ES:[DI]
		JNE SEGUNDO
		INC AX
	SEGUNDO:
		CMP BL, ES:[DI+1]
		JNE TERCERO
		INC AX
	TERCERO:
		CMP CH, ES:[DI+2]
		JNE CUARTO
		INC AX
	CUARTO:
		CMP CL, ES:[DI+3]
		JNE FIN
		INC AX
	FIN:
		
		POP CX BX DI BP
		RET ;DEVUELVE EL CONTENIDO DEL CONTADOR (AX)
	_calcularAciertos ENDP
	
	_calcularSemiaciertos PROC FAR
	PUSH BP
	MOV BP, SP ;COLOCAMOS EL PUNTERO EN LA CIMA DE LA PILA
	PUSH DI BX CX ;GUARDAMOS LOS REGISTROS USADOS
	
	;METEMOS EL INTENTO EN ES
	LES DI, [BP+6] ;SEGMENT=>ES OFFSET=>DI
	
	;PASAMOS EL NUMERO DEL INTENTO A REGISTROS
	MOV BH, ES:[DI] 
	MOV BL, ES:[DI+1]
	MOV CH, ES:[DI+2]
	MOV CL, ES:[DI+3]
	
	;METEMOS EL NUMERO SECRETO EN ES
	LES DI, [BP+10] ;SEGMENT=>ES OFFSET=>DI
	
	MOV AX, 0; PONEMOS EL CONTADOR A 0
	
	;PRIMER DIGITO INTENTO
	CMP BH, ES:[DI] ;COMPROBAMOS SI ES ACIERTO PARA NO HACER MAS COMPROBACIONES
	JE SEGUNDODIGITO
	CMP BH, ES:[DI+1] ;SI COINCIDE CON EL SEGUNDO DIGITO DEL NUMERO SECRETO
	JE SEMIPRIMERO    ;SUMAMOS UN SEMIACIERTO
	CMP BH, ES:[DI+2] ;SI COINCIDE CON EL TERCER DIGITO DEL NUMERO SECRETO
	JE SEMIPRIMERO    ;SUMAMOS UN SEMIACIERTO
	CMP BH, ES:[DI+3] ;SI NO COINCIDE CON EL CUARTO DIGITO DEL NUMERO SECRETO
	JNE SEGUNDODIGITO ;SALTAMOS AL SIGUIENTE DIGITO
	
	SEMIPRIMERO:
	INC AX
	
	;SEGUNDO DIGITO INTENTO
	SEGUNDODIGITO:
	CMP BL, ES:[DI+1] ;COMPROBAMOS SI ES ACIERTO PARA NO HACER MAS COMPROBACIONES
	JE TERCERDIGITO
	CMP BL, ES:[DI]   ;SI COINCIDE CON EL PRIMER DIGITO DEL NUMERO SECRETO
	JE SEMISEGUNDO    ;SUMAMOS UN SEMIACIERTO
	CMP BL, ES:[DI+2] ;SI COINCIDE CON EL TERCER DIGITO DEL NUMERO SECRETO
	JE SEMISEGUNDO    ;SUMAMOS UN SEMIACIERTO
	CMP BL, ES:[DI+3] ;SI NO COINCIDE CON EL CUARTO DIGITO DEL NUMERO SECRETO
	JNE TERCERDIGITO  ;SALTAMOS AL SIGUIENTE DIGITO

	SEMISEGUNDO:
	INC AX

	;TERCERDIGITO INTENTO
	TERCERDIGITO:
	CMP CH, ES:[DI+2] ;COMPROBAMOS SI ES ACIERTO PARA NO HACER MAS COMPROBACIONES
	JE CUARTODIGITO
	CMP CH, ES:[DI]   ;SI COINCIDE CON EL PRIMER DIGITO DEL NUMERO SECRETO
	JE SEMITERCERO    ;SUMAMOS UN SEMIACIERTO
	CMP CH, ES:[DI+1] ;SI COINCIDE CON EL SEGUNDO DIGITO DEL NUMERO SECRETO
	JE SEMITERCERO    ;SUMAMOS UN SEMIACIERTO
	CMP CH, ES:[DI+3] ;SI NO COINCIDE CON EL CUARTO DIGITO DEL NUMERO SECRETO
	JNE CUARTODIGITO  ;SALTAMOS AL SIGUIENTE DIGITO
	
	SEMITERCERO:
	INC AX
	
	;CUARTODIGITO INTENTO
	CUARTODIGITO:
	CMP CL, ES:[DI+3] ;COMPROBAMOS SI ES ACIERTO PARA NO HACER MAS COMPROBACIONES
	JE FINAL
	CMP CL, ES:[DI]   ;SI COINCIDE CON EL PRIMER DIGITO DEL NUMERO SECRETO
	JE SEMICUARTO     ;SUMAMOS UN SEMIACIERTO
	CMP CL, ES:[DI+1] ;SI COINCIDE CON EL SEGUNDO DIGITO DEL NUMERO SECRETO
	JE SEMICUARTO     ;SUMAMOS UN SEMIACIERTO
	CMP CL, ES:[DI+2] ;SI NO COINCIDE CON EL TERCER DIGITO DEL NUMERO SECRETO
	JNE FINAL         ;TERMINAMOS
		
	SEMICUARTO:
	INC AX	
	
	FINAL:
	POP CX BX DI BP
	RET
	_calcularSemiaciertos ENDP
	
PRACT3B ENDS
END